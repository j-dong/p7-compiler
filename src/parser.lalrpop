// vim:ft=rust
use std::str::FromStr;
use ast::*;
use ast::Global::*;
use ast::Statement::*;
use ast::Expr::*;

grammar;

Ident: String = <s:r"[a-zA-Z_][a-zA-Z0-9_]*"> => s.to_string();
String: String = <s:r#""([^"\\]|\\.)*""#> => s.to_string(); // TODO: process escapes
Number: i16 = <s:r"[0-9]+"> => i16::from_str(s).unwrap();

pub Program: Vec<Global> = {
    Global*,
};

pub Global: Global = {
    "global" <Ident> ";" => GlobalVariable(<>),
    <Ident> "(" <Ident*> ")" <Statement> => Function(<>),
};

pub Statement: Statement = {
    <Expr> ";" => ExprStatement(<>),
    "{" <Statement*> "}" => BlockStatement(<>),
    "if" "(" <cond:Expr> ")" <body:Statement> => IfStatement { cond, body: Box::new(body) },
    "while" "(" <cond:Expr> ")" <body:Statement> => WhileStatement { cond, body: Box::new(body) },
    "return" <Expr?> ";" => ReturnStatement(<>),
    "break" ";" => BreakStatement,
    "continue" ";" => ContinueStatement,
    "print" <String> ";" => PrintStatement(<>),
};

pub Expr: Box<Expr> = {
    Assignment,
};

pub Assignment: Box<Expr> = {
    <Ident> "=" <Assignment> => Box::new(AssignExpr(<>)),
    Sum,
};

pub Sum: Box<Expr> = {
    <Sum> "+" <Term> => Box::new(Plus(<>)),
    <Sum> "-" <Term> => Box::new(Minus(<>)),
    Term,
};

pub Term: Box<Expr> = {
    "(" <Assignment> ")",
    Ident  => Box::new(Variable(<>)),
    Number => Box::new(Constant(<>)),
};
